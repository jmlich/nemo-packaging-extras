# $Id$

_host="github.com"
_project=mapbox
_basename=mapbox-gl-native
_branch=master

_gittag=maps-v1.6.0
_gitname=$_basename
pkgname=$_basename-git

pkgver=maps.v1.6.0.r0.gd60fd302b1

pkgrel=1
pkgdesc="Mapbox GL Native Qt version"
arch=('x86_64' 'aarch64')
#url="https://$_host/$_project/$_gitname#branch=$_branch"
url="https://$_host/$_project/$_gitname#tag=${_gittag}"
license=('2-Clause BSD license')
depends=('qt5-base' 'sqlite' 'zlib' 'curl' 'icu' )
makedepends=('git' 'cmake')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}"
    '0001-add-include.patch'
)
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
    cd "$srcdir/${pkgname}"
    git submodule update --init --recursive

    patch -p1 --input="${srcdir}/0001-add-include.patch"
}

build() {
  mkdir -p "${srcdir}/${pkgname}/build"
  cd "${srcdir}/${pkgname}/build"

    cmake \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DMBGL_WITH_QT=ON -DMBGL_WITH_WERROR=OFF  \
        -DMBGL_WITH_QT_HEADLESS=OFF -DMBGL_WITH_QT_LIB_ONLY=ON \
        -DCMAKE_SHARED_LINKER_FLAGS=-pthread \
        ..
    make
}

package() {
    make -C "${srcdir}/${pkgname}/build" DESTDIR="$pkgdir" install
}
