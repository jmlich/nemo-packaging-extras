# $Id$

_host="github.com"
_project=google
_basename=s2geometry
_branch=master

_gitname=$_basename
pkgname=$_basename-git

pkgver=v0.9.0.r40.g7f0dc0b

pkgrel=1
pkgdesc="S2 is primarily designed to work with spherical geometry,"
arch=('x86_64' 'aarch64')
url="https://$_host/$_project/$_gitname#branch=$_branch"
license=('Apache-2.0 License')
depends=('qt5-base' 'abseil-cpp' 'openssl' 'gtest')
makedepends=('git' 'cmake')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+${url}"
    '0001-add-hash.h-include.patch'
)
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/${pkgname}"
  ( set -o pipefail
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  ) 2>/dev/null
}

prepare() {
    cd "$srcdir/${pkgname}"
    patch -p1 --input="${srcdir}/0001-add-hash.h-include.patch"
}

build() {
    cmake \
        -B "${pkgname}/build" \
        -S "${pkgname}" \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DCMAKE_VERBOSE_MAKEFILE=ON -DBUILD_PYTHON=OFF -DBUILD_TESTING=OFF \
        -DBUILD_SHARED_LIBS=ON -DBUILD_EXAMPLES=OFF
    make -C "${pkgname}/build" all
}

package() {
    make -C "${srcdir}/${pkgname}/build" DESTDIR="$pkgdir" install
}
